ext {
    buildLabel = 'Developer-1'
    buildData = new Date().format("dd/MM/yyyy HH:mm")
    apiName = 'data-quality-api'
    apiPackageName = 'DataQualityApi'
}

buildscript {
    ext {
        springBootVersion = '1.5.2'
    }
    repositories {
        // You can declare any Maven/Ivy/file repository here.
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        // Use 'jcenter' for resolving your dependencies.
        jcenter()
    }
    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}.RELEASE"
        )
    }
}

allprojects {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
    }

    // Apply the java plugin to add support for Java
    apply plugin: 'java'
    apply plugin: 'idea'
}

def javaSubProjects() {
    subprojects.findAll { it.name != apiName }
}

configure(javaSubProjects()) {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

idea.project {
    jdkName = '1.8'
    languageLevel = '1.8'
    ipr {
        withXml {
            it.node.component.find { it.@name == 'VcsDirectoryMapping' }.mapping.@vcs = 'Git'
        }
    }
}


